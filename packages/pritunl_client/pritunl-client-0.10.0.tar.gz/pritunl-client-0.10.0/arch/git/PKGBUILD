# Maintainer: Zachary Huff <zach.huff.386@gmail.com>

pkgname=pritunl-client-git
_pkgname=pritunl-client
pkgver=0.0.0
pkgrel=1
pkgdesc="Pritunl openvpn client (git version)"
arch=("any")
license=("AGPL3")
url="https://github.com/pritunl/${_pkgname}"
depends=(
    "python2"
    "pygtk"
    "polkit"
    "net-tools"
    "iproute2"
    "openvpn"
)
makedepends=(
    "git"
    "python2-distribute"
    "pygtk"
)
provides=("${pkgname}")
conflicts=("${_pkgname}")
install=${_pkgname}.install
source=("git+${url}.git")
sha256sums=("SKIP")
options=("emptydirs")

pkgver() {
  cd "$srcdir/$_pkgname"
  git log -1 --format=%at
}

build() {
    cd "${srcdir}/${_pkgname}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}"
    mkdir -p "${pkgdir}/etc/pritunl_client"
    python2 setup.py install --root="${pkgdir}" --prefix=/usr
}
